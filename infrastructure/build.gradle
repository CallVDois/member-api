buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.flywaydb:flyway-database-postgresql:11.8.2'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '3.4.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.flywaydb.flyway' version '11.8.2'
}

group = 'com.callv2'

bootJar {
    archiveFileName = 'application.jar'
    destinationDirectory = file("${rootProject.buildDir}/libs")
}

repositories {
    mavenCentral()
}

dependencies {

    implementation(project(":domain"))
    implementation(project(":application"))
    

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'org.springframework.boot:spring-boot-starter-amqp'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.postgresql:postgresql:42.7.5'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.1'

    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.19.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation('org.flywaydb:flyway-core')
    testImplementation('org.flywaydb:flyway-database-postgresql')
    
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.15.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    
    implementation libs.guava
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

compileJava {
    options.compilerArgs += [
            '-Amapstruct.defaultComponentModel=spring'
    ]
}

tasks.named('test') {
    useJUnitPlatform()
}

flyway {
    url = System.getenv('FLYWAY_DB') ?: 'jdbc:postgresql://localhost:5432/postgres'
    user = System.getenv('FLYWAY_USER') ?: 'postgres'
    password = System.getenv('FLYWAY_PASS') ?: 'postgres'
    cleanDisabled = false
}